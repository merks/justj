// Uses Declarative syntax to run commands inside a container.
pipeline {
  agent { label 'migration' }

  options {
    buildDiscarder(logRotator(numToKeepStr: '10'))
    disableConcurrentBuilds()
  }

  tools {
    maven 'apache-maven-latest'
    jdk 'oracle-jdk8-latest'
  }

  environment {
    JUSTJ_BUILD_TIME_STAMP = sh(returnStdout: true, script: 'date +%Y%m%d_%H%M').trim()
    PUBLISH_LOCATION = 'sandbox/jres/updates'
  }

  parameters {
    string(
      name: 'JRE_URL_WINDOWS',
      defaultValue: 'https://download.java.net/java/GA/jdk14.0.1/664493ef4a6946b186ff29eb326336a2/7/GPL/openjdk-14.0.1_windows-x64_bin.zip',
      description: 'The URL to the Windows zip of a JDK')

    string(
      name: 'JRE_URL_MACOS',
      defaultValue: 'https://download.java.net/java/GA/jdk14.0.1/664493ef4a6946b186ff29eb326336a2/7/GPL/openjdk-14.0.1_osx-x64_bin.tar.gz',
      description: 'The URL to the MacOS tar.gz of a JDK')

    string(
      name: 'JRE_URL_LINUX',
      defaultValue: 'https://download.java.net/java/GA/jdk14.0.1/664493ef4a6946b186ff29eb326336a2/7/GPL/openjdk-14.0.1_linux-x64_bin.tar.gz',
      description: 'The URL to the Linix tar.gz of a JDK')

    choice(
      name: 'BUILD_TYPE',
      choices: ['nightly', 'milestone', 'release'],
      description: '''
        This is the description.
        ''')

    booleanParam(
      name: 'PROMOTE',
      defaultValue: false,
      description: 'Whether to promote the p2 repository to the download server.'
    )
  }

  stages {
    stage('Setup Environment') {
      steps {
        sh '''
        echo 'Setting up environment...'
        ls -R releng
        chmod +x releng/org.eclipse.justj.releng/*.sh
        '''
        stash includes: 'releng/org.eclipse.justj.releng/**', name: 'releng-stash'
        script {
          env.JRE_URL_WINDOWS = params.JRE_URL_WINDOWS
          env.JRE_URL_MACOS = params.JRE_URL_MACOS
          env.JRE_URL_LINUX = params.JRE_URL_LINUX
          env.BUILD_TYPE = params.BUILD_TYPE
          env.PROMOTE = params.PROMOTE
        }
      }
    }

    stage('Build JREs') {
      parallel {

        stage('Build Linux JRES') {
          when {
            not { environment name: 'JRE_URL_LINUX', value: '' }
          }
          steps {
            sh '''
            releng/org.eclipse.justj.releng/build-jre.sh
            '''
          }
        }

        stage('Build Windows JREs') {
          agent {
            label 'windows'
          }

          when {
            beforeAgent true
            not { environment name: 'JRE_URL_WINDOWS', value: '' }
          }

          options { skipDefaultCheckout true }

          steps {
            cleanWs()
            unstash 'releng-stash'
            bat '''
              bash -ex releng/org.eclipse.justj.releng/build-jre.sh
              '''
            stash includes: '*org.eclipse.justj.*.tar.gz', name: 'win-stash'
          }
        }

        stage('Build MacOS JREs') {
          agent {
            label 'macos'
          }

          when {
            beforeAgent true
            not { environment name: 'JRE_URL_MACOS', value: '' }
          }

          options { skipDefaultCheckout true }

          steps {
            cleanWs()
            unstash 'releng-stash'
            sh '''
            releng/org.eclipse.justj.releng/build-jre.sh
            '''
            stash includes: 'org.eclipse.justj.*.tar.gz', name: 'mac-stash'
          }
        }
      }
    }

    stage('Compose JREs') {
      steps {
        script {
          if (env.JRE_URL_MACOS != '') {
            unstash 'mac-stash'
          }
    
          if (env.JRE_URL_WINDOWS != '') {
            unstash 'win-stash'
          }
        }

        sh 'ls org.eclipse.justj.*.tar.gz > justj.manifest'
        dir('releng/org.eclipse.justj.releng/index') {
          sh '''
            mvn -Djustj.manifest.location=../../../justj.manifest -Djustj.manifest.url=https://download.eclipse.org/justj/sandbox/jres/$JUSTJ_BUILD_TIME_STAMP/justj.manifest clean verify
            '''
        }

        archiveArtifacts 'org.eclipse.justj.*.tar.gz'
        archiveArtifacts 'justj.manifest'
        archiveArtifacts 'index.html'

        sshagent(['projects-storage.eclipse.org-bot-ssh']) {
          sh '''
            ssh genie.justj@projects-storage.eclipse.org "
            ls -al /home/data/httpd/download.eclipse.org/justj/sandbox/jres
            mkdir -p /home/data/httpd/download.eclipse.org/justj/sandbox/jres/$JUSTJ_BUILD_TIME_STAMP
            "

            scp index.html justj.manifest org.eclipse.justj.*.tar.gz genie.justj@projects-storage.eclipse.org:/home/data/httpd/download.eclipse.org/justj/sandbox/jres/$JUSTJ_BUILD_TIME_STAMP

            ssh genie.justj@projects-storage.eclipse.org "
            mkdir -p /home/data/httpd/download.eclipse.org/justj/sandbox/jres/latest
            cd /home/data/httpd/download.eclipse.org/justj/sandbox/jres/latest
            ls ../$JUSTJ_BUILD_TIME_STAMP/org.eclipse.justj.*.tar.gz > justj.manifest
            cp ../$JUSTJ_BUILD_TIME_STAMP/index.html .
            "
            '''
        }
      }
    }

    stage('Reconcile/Generate JRE Model') {
      steps {
        dir('model/org.eclipse.justj.model') {
          sh '''
            sed 's%source=".*[.]manifest"%source="https://download.eclipse.org/justj/sandbox/jres/'$JUSTJ_BUILD_TIME_STAMP'/justj.manifest"%g' < justj.jregen > justj-transformed.jregen
            cat justj-transformed.jregen
            mv justj-transformed.jregen justj.jregen
            mvn clean verify
          '''
          archiveArtifacts '*-gen/'
        }
      }
    }

    stage('Build JRE p2 Repository') {
      steps {
        sshagent(['projects-storage.eclipse.org-bot-ssh']) {
          dir('model/org.eclipse.justj.model/jre-gen') {
            sh '''
              if [[ $PROMOTE == false ]]; then
                promotion_argument='-Dorg.eclipse.justj.p2.manager.args='
              fi
              mvn $promotion_argument -DPACK_AND_SIGN=true -Dorg.eclipse.justj.p2.manager.build.url=$JOB_URL -Dorg.eclipse.justj.p2.manager.relative=$PUBLISH_LOCATION -Dbuild.type=$BUILD_TYPE -Dgit.commit=$GIT_COMMIT -Dbuild.id=$BUILD_NUMBER clean verify
              '''
            dir('releng/org.eclipse.justj.site/target') {
              archiveArtifacts 'repository/,org.eclipse.justj.site*.zip'
            }
          }
        }
      }
    }
  }

  post {
    failure {
      mail to: 'ed.merks@gmail.com',
      subject: "[JustJ CI] Build Failure ${currentBuild.fullDisplayName}",
      mimeType: 'text/html',
      body: "Project: ${env.JOB_NAME}<br/>Build Number: ${env.BUILD_NUMBER}<br/>Build URL: ${env.BUILD_URL}<br/>Console: ${env.BUILD_URL}/console"
    }
    fixed {
      mail to: 'ed.merks@gmail.com',
      subject: "[JustJ CI] Back to normal ${currentBuild.fullDisplayName}",
      mimeType: 'text/html',
      body: "Project: ${env.JOB_NAME}<br/>Build Number: ${env.BUILD_NUMBER}<br/>Build URL: ${env.BUILD_URL}<br/>Console: ${env.BUILD_URL}/console"
    }
    cleanup {
      deleteDir()
    }
  }
}
